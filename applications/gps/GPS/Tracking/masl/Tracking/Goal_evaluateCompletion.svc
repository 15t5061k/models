public instance service Tracking::Goal.evaluateCompletion () is
goalSpec: instance of GoalSpec;
elapsedSpan: real;
session: instance of WorkoutSession;
workoutTimer: instance of WorkoutTimer;
openAchievement: instance of Achievement;
begin
  // Based on the span type for this goal, determine 
  //  whether it has been completed, update achievement
  //  records as necessary, and advance to the next goal
  //  if appropriate.
  
  goalSpec := this->R9.specified_by.GoalSpec;
  
  // Based on the span type for the goal, get the elapsed span 
  //   for comparison against the specified span for the goal.
  elapsedSpan := 0.0;  // Explicit declaration
  if ( goalSpec.spanType = Distance ) then
    session := this->R11.is_currently_executing_within.WorkoutSession;
    elapsedSpan := session.accumulatedDistance - this.startingPoint;
  elsif ( goalSpec.spanType = Time ) then
    workoutTimer := this->R11.is_currently_executing_within.WorkoutSession->R8.is_timed_by.WorkoutTimer;
    elapsedSpan := real(workoutTimer.time) - this.startingPoint;
  else
    LOG::LogFailure( "Goal.evaluateCompletion: Unknown Goal Span Type." );
  end if;
  
  // Compare the current value against the specified span to determine 
  //   whether the execution of this workout goal is complete.
  if ( elapsedSpan >= goalSpec.span ) then
    openAchievement := this->R14.has_open.Achievement;
    if ( null /= openAchievement ) then
      openAchievement.close();
    end if;
    generate Goal.Completed() to this;
  end if;
end service;
