public instance service Tracking::Goal.calculateStart () is
goalSpec: instance of GoalSpec;
workoutTimer: instance of WorkoutTimer;
session: instance of WorkoutSession;
begin
  // Based on the span type for this goal, calculate and
  //   store the value of the start attribute.
  goalSpec := this->R9.specified_by.GoalSpec;
  if ( goalSpec.spanType = Time ) then
    workoutTimer := this->R11.is_currently_executing_within.WorkoutSession->R8.is_timed_by.WorkoutTimer;
    this.startingPoint := real(workoutTimer.time);
  elsif ( goalSpec.spanType = Distance ) then
    session := this->R11.is_currently_executing_within.WorkoutSession;
    this.startingPoint := session.accumulatedDistance;
  else
    LOG::LogFailure( "Goal.calculateStart: Unknown Goal Span Type." );
  end if;
end service;
