public service Tracking::WorkoutSession.initialize () is
session: instance of WorkoutSession;
workoutTimer: instance of WorkoutTimer;
trackLog: instance of TrackLog;
display: instance of Display;
begin
  // Unless a workout session already exists, create and initialize a 
  // workout session, workout timer, and tracklog, and relate them all.
  
  session := find_one WorkoutSession();
  
  if ( null = session ) then
  
    // Create a workout session.
    session := create WorkoutSession( startTime => timestamp'now() );
    // Initialize this workout session.
    session.accumulatedDistance := 0.0;
  
    // Create a workout timer.
    workoutTimer := create WorkoutTimer(  session_startTime => session.startTime, Current_State => stopped );
    workoutTimer.initialize();
  
    // Create a track log.
    trackLog := create TrackLog(  session_startTime => session.startTime );
    
    // Create a display.
    display := create Display( session_startTime => session.startTime, Current_State => displayDistance );
  
    // Relate them all together.
    link trackLog R4.represents_path_for session;
    link workoutTimer R8.acts_as_the_stopwatch_for session;
    link display R7.indicates_current_status_of session;
    
  end if;
end service;
